/**
 * Custom font asset source folder - only includes used fonts
 */
android.sourceSets.main.assets.srcDirs += file("$buildDir/intermediates/ReactNativeVectorIcons")

/**
 * Task to copy only the used icon font files
 */
afterEvaluate {
    def iconFontsDir = "../../node_modules/react-native-vector-icons/Fonts";
    
    // Only include the fonts that are actually used in the app
    def usedFonts = [
        "Ionicons.ttf",
        "MaterialCommunityIcons.ttf", 
        "MaterialIcons.ttf",
        "AntDesign.ttf",
        "Entypo.ttf"
    ];

    def fontCopyTask = tasks.create(
        name: "copyReactNativeVectorIconFonts",
        type: Copy) {
        description = "copy only used vector icon fonts."
        into "$buildDir/intermediates/ReactNativeVectorIcons/fonts"

        from(iconFontsDir) {
            include(usedFonts)
        }
    }

    android.applicationVariants.all { def variant ->
        def targetName = variant.name.capitalize()
        def lintVitalAnalyzeTask = tasks.findByName("lintVitalAnalyze${targetName}")
      
        if (lintVitalAnalyzeTask) {
            lintVitalAnalyzeTask.dependsOn(fontCopyTask)
        }

        def generateReportTask = tasks.findByName("generate${targetName}LintVitalReportModel")
        if (generateReportTask) {
            generateReportTask.dependsOn(fontCopyTask)
        }
      
        def generateAssetsTask = tasks.findByName("generate${targetName}Assets")
        generateAssetsTask.dependsOn(fontCopyTask)
    }
} 